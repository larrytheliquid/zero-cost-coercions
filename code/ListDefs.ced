import Nat.
import List.
import VecL.
import IdCoe.
import NestIdCoe.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

appendL ◂ ∀ A : ★ . List · A ➔ List · A ➔ List · A
  = Λ A . elimList' · A ·
  (λ xs : List · A . List · A ➔ List · A)
  (λ ys . ys)
  (Λ xs . λ x . λ ih . λ ys . consL · A x (ih ys)) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

appendAssocL ◂ ∀ A : ★ .
  Π xs : List · A .
  Π ys : List · A .
  Π zs : List · A .
  appendL (appendL xs ys) zs ≃ appendL xs (appendL ys zs)
  = Λ A . elimList' · A ·
  (λ xs : List · A .
    Π ys : List · A .
    Π zs : List · A .
    appendL (appendL xs ys) zs ≃ appendL xs (appendL ys zs))
  (λ ys . λ zs . β)
  (Λ xs . λ x . λ ih . λ ys . λ zs .
    ρ+ (ih ys zs) - β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

lengthDistAppend ◂ ∀ A : ★ .
  Π xs : List · A .
  Π ys : List · A .
  add (length · A  xs) (length · A ys) ≃ length (appendL · A xs ys)
  = Λ A . elimList' · A ·
  (λ xs : List · A . Π ys : List · A .
    add (length xs) (length ys) ≃ length (appendL xs ys))
  (λ ys . β)
  (Λ xs . λ x . λ ih . λ ys . ρ+ (ih ys) - β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

concatL ◂ ∀ A : ★ . List · (List · A) ➔ List · A
  = Λ A . elimList' · (List · A) ·
  (λ xss : List · (List · A) . List · A)
  (nilL · A)
  (Λ xss . λ xs . λ ih . appendL · A xs ih) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

lengthDistConcat ◂ ∀ A : ★ . ∀ n : Nat .
  Π xss : List · (VecL · A n) .
  mult (length · (VecL · A n) xss) n ≃ length (concatL · A (u2l-l2l · A -n xss))
  = Λ A . Λ n . elimList' · (VecL · A n) ·
  (λ xss : List · (VecL · A n) . mult (length xss) n ≃ length (concatL xss))
  β
  (Λ xss . λ xs . λ ih .
    ρ+ ih -
    ρ xs.2 -
    ρ+ ς (lengthDistAppend · A xs.1 (concatL · A (u2l-l2l · A -n xss))) -
    β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

concatDistAppendL ◂ ∀ A : ★ .
  Π xss : List · (List · A) .
  Π yss : List · (List · A) .
  appendL (concatL xss) (concatL yss) ≃ concatL (appendL xss yss)
  = Λ A . elimList' · (List · A) ·
  (λ xss : List · (List · A) . Π yss : List · (List · A) .
    appendL (concatL xss) (concatL yss) ≃ concatL (appendL xss yss))
  (λ yss . β)
  (Λ xss . λ xs . λ ih . λ yss .
    ρ+ ς (ih yss) -
    ρ (appendAssocL · A xs (concatL · A xss) (concatL · A yss)) -
    β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

