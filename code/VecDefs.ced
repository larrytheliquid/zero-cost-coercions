import Nat.
import Vec.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

appendV ◂ ∀ A : ★ .
  ∀ n : Nat . Vec · A n ➔
  ∀ m : Nat . Vec · A m ➔
  Vec · A (add n m)
  = Λ A . elimVec' · A ·
  (λ n : Nat . λ xs : Vec · A n .
    ∀ m : Nat . Vec · A m ➔ Vec · A (add n m))
  (Λ m . λ ys . ys)
  (Λ n . Λ xs . λ x . λ ih . Λ m . λ ys . consV · A -(add n m) x (ih -m ys)) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

appendAssocV ◂ ∀ A : ★ .
  ∀ n : Nat . Π xs : Vec · A n .
  ∀ m : Nat . Π ys : Vec · A m .
  ∀ o : Nat . Π zs : Vec · A o .
  appendV (appendV xs ys) zs ≃ appendV xs (appendV ys zs)
  = Λ A . elimVec' · A ·
  (λ n : Nat . λ xs : Vec · A n .
    ∀ m : Nat . Π ys : Vec · A m .
    ∀ o : Nat . Π zs : Vec · A o .
    appendV (appendV xs ys) zs ≃ appendV xs (appendV ys zs))
  (Λ m . λ ys . Λ o . λ zs . β)
  (Λ n . Λ xs . λ x . λ ih . Λ m . λ ys . Λ o . λ zs .
    ρ+ (ih -m ys -o zs) - β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

concatV ◂ ∀ A : ★ . ∀ m : Nat . ∀ n : Nat .
  Vec · (Vec · A m) n ➔ Vec · A (mult n m)
  = Λ A . Λ m . elimVec' · (Vec · A m) ·
  (λ n : Nat . λ xs : Vec · (Vec · A m) n . Vec · A (mult n m))
  (nilV · A)
  (Λ n . Λ xss . λ xs . λ ih . appendV · A -m xs -(mult n m) ih) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

concatDistAppendV ◂ ∀ A : ★ .
  ∀ n1 : Nat . ∀ n2 : Nat . Π xss : Vec · (Vec · A n1) n2 .
  ∀ m1 : Nat . ∀ m2 : Nat . Π yss : Vec · (Vec · A m1) m2 .
  appendV (concatV xss) (concatV yss) ≃ concatV (appendV xss yss)
  = Λ A . Λ n1 . elimVec' · (Vec · A n1) ·
  (λ n2 : Nat . λ xss : Vec · (Vec · A n1) n2 .
    ∀ m1 : Nat . ∀ m2 : Nat . Π yss : Vec · (Vec · A m1) m2 .
    appendV (concatV xss) (concatV yss) ≃ concatV (appendV xss yss))
  (Λ m1 . Λ m2 . λ yss . β)
  (Λ n2 . Λ xss . λ xs . λ ih . Λ m1 . Λ m2 . λ yss .
    ρ+ ς (ih -m1 -m2 yss) -
    ρ (appendAssocV · A
      -n1 xs
      -(mult n2 n1) (concatV · A -n1 -n2 xss)
      -(mult m2 m1) (concatV · A -m1 -m2 yss)) -
    β) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

